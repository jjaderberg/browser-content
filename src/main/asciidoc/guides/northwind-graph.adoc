[[northwind-graph]]
=== Northwind Graph

From RDBMS to Graph, using a classic dataset

The__Northwind Graph__ demonstrates how to migrate from a relational
database to Neo4j. The transformation is iterative and deliberate,
emphasizing the conceptual shift from relational tables to the nodes and
relationships of a graph.

This guide will show you how to:

1.  Load: create data from external CSV files
2.  Index: index nodes based on label
3.  Relate: transform foreign key references into data relationships
4.  Promote: transform join records into relationships

[[product-catalog]]
=== Product Catalog

Northwind sells food products in a few categories, provided by
suppliers. Let's start by loading the product catalog tables.

The load statements to the right require public internet
access.`LOAD CSV` will retrieve a CSV file from a valid URL, applying a
Cypher statement to each row using a named map (here we're using the
name `row`).

image:images/northwind/product-category-supplier.png[image]

'''''

:help cypher LOAD CSV

[[load-records]]
==== Load records

code,pre-scrollable,code,runnable--------------------------------------------------------------------------------------
code,pre-scrollable,code,runnable
LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/products.csv" AS row
CREATE (n:Product)
SET n = row,
  n.unitPrice = toFloat(row.unitPrice),
  n.unitsInStock = toInt(row.unitsInStock), n.unitsOnOrder = toInt(row.unitsOnOrder),
  n.reorderLevel = toInt(row.reorderLevel), n.discontinued = (row.discontinued <> "0")
--------------------------------------------------------------------------------------

code,pre-scrollable,code,runnable----------------------------------------------------------------------------------
code,pre-scrollable,code,runnable
LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/categories.csv" AS row
CREATE (n:Category)
SET n = row
----------------------------------------------------------------------------------

code,pre-scrollable,code,runnable---------------------------------------------------------------------------------
code,pre-scrollable,code,runnable
LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/suppliers.csv" AS row
CREATE (n:Supplier)
SET n = row
---------------------------------------------------------------------------------

[[create-indexes]]
==== Create indexes

code,pre-scrollable,code,runnable-----------------------------------
code,pre-scrollable,code,runnable
CREATE INDEX ON :Product(productID)
-----------------------------------

code,pre-scrollable,code,runnable-------------------------------------
code,pre-scrollable,code,runnable
CREATE INDEX ON :Category(categoryID)
-------------------------------------

code,pre-scrollable,code,runnable-------------------------------------
code,pre-scrollable,code,runnable
CREATE INDEX ON :Supplier(supplierID)
-------------------------------------

[[product-catalog-graph]]
=== Product Catalog Graph

The products, categories and suppliers are related through foreign key
references. Let's promote those to data relationships to realize the
graph.

image:images/northwind/product-graph.png[image]

'''''

:help cypher MATCH

[[create-data-relationships]]
==== Create data relationships

code,pre-scrollable,code,runnable---------------------------------
code,pre-scrollable,code,runnable
MATCH (p:Product),(c:Category)
WHERE p.categoryID = c.categoryID
CREATE (p)-[:PART_OF]->(c)
---------------------------------

Note you only need to compare property values like this when first
creating relationships

Calculate join, materialize relationship.
(See http://neo4j.com/developer/guide-importing-data-and-etl[  importing
guide] for more details)

code,pre-scrollable,code,runnable---------------------------------
code,pre-scrollable,code,runnable
MATCH (p:Product),(s:Supplier)
WHERE p.supplierID = s.supplierID
CREATE (s)-[:SUPPLIES]->(p)
---------------------------------

Note you only need to compare property values like this when first
creating relationships

[[querying-product-catalog-graph]]
=== Querying Product Catalog Graph

Lets try some queries using patterns.

image:images/northwind/product-graph.png[image]

'''''

:help cypher MATCH

[[query-using-patterns]]
==== Query using patterns

code,pre-scrollable,code,runnable-------------------------------------------------------------------------------
code,pre-scrollable,code,runnable
MATCH (s:Supplier)-->(:Product)-->(c:Category)
RETURN s.companyName as Company, collect(distinct c.categoryName) as Categories
-------------------------------------------------------------------------------

List the product categories provided by each supplier.

code,pre-scrollable,code,runnable-----------------------------------------------------------------------
code,pre-scrollable,code,runnable
MATCH (c:Category {categoryName:"Produce"})<--(:Product)<--(s:Supplier)
RETURN DISTINCT s.companyName as ProduceSuppliers
-----------------------------------------------------------------------

Find the produce suppliers.

[[customer-orders]]
=== Customer Orders

Northwind customers place orders which may detail multiple
products.image:images/northwind/customer-orders.png[image]

'''''

:help cypher LOAD CSV

[[load-and-index-records]]
==== Load and index records

code,pre-scrollable,code,runnable---------------------------------------------------------------------------------
code,pre-scrollable,code,runnable
LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/customers.csv" AS row
CREATE (n:Customer)
SET n = row
---------------------------------------------------------------------------------

code,pre-scrollable,code,runnable------------------------------------------------------------------------------
code,pre-scrollable,code,runnable
LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/orders.csv" AS row
CREATE (n:Order)
SET n = row
------------------------------------------------------------------------------

code,pre-scrollable,code,runnable-------------------------------------
code,pre-scrollable,code,runnable
CREATE INDEX ON :Customer(customerID)
-------------------------------------

code,pre-scrollable,code,runnable-------------------------------
code,pre-scrollable,code,runnable
CREATE INDEX ON :Order(orderID)
-------------------------------

[[create-data-relationships-1]]
==== Create data relationships

code,pre-scrollable,code,runnable---------------------------------
code,pre-scrollable,code,runnable
MATCH (c:Customer),(o:Order)
WHERE c.customerID = o.customerID
CREATE (c)-[:PURCHASED]->(o)
---------------------------------

Note you only need to compare property values like this when first
creating relationships

[[customer-order-graph]]
=== Customer Order Graph

Notice that Order Details are always part of an Order and that
they__relate__ the Order to a Product — they're a join table. Join
tables are always a sign of a data relationship, indicating shared
information between two other records.

Here, we'll directly promote each OrderDetail record into a relationship
in the graph.image:images/northwind/order-graph.png[image]

'''''

:help cypher LOAD CSV

[[load-and-index-records-1]]
==== Load and index records

code,pre-scrollable,code,runnable-------------------------------------------------------------------------------------
code,pre-scrollable,code,runnable
LOAD CSV WITH HEADERS FROM "http://data.neo4j.com/northwind/order-details.csv" AS row
MATCH (p:Product), (o:Order)
WHERE p.productID = row.productID AND o.orderID = row.orderID
CREATE (o)-[details:ORDERS]->(p)
SET details = row,
  details.quantity = toInt(row.quantity)
-------------------------------------------------------------------------------------

Note you only need to compare property values like this when first
creating relationships

[[query-using-patterns-1]]
==== Query using patterns

code,pre-scrollable,code,runnable-------------------------------------------------------------------------------------------
code,pre-scrollable,code,runnable
MATCH (cust:Customer)-[:PURCHASED]->(:Order)-[o:ORDERS]->(p:Product),
      (p)-[:PART_OF]->(c:Category {categoryName:"Produce"})
RETURN DISTINCT cust.contactName as CustomerName, SUM(o.quantity) AS TotalProductsPurchased
-------------------------------------------------------------------------------------------

[[northwind-graph-1]]
==== Northwind Graph

 +

[[next-steps]]
=== Next steps

[[more-code]]
=== More code

* Movie Graph - actors & movies
* Query Templates - common ad-hoc queries
* Cypher - query language fundamentals

[[reference]]
=== Reference

* http://neo4j.com/developer/guide-importing-data-and-etl/[Full
Northwind import example]
* http://neo4j.com/developer[Developer resources]
* http://neo4j.com/docs/%7B%7Bneo4j.version%20%7C%20neo4jdoc%20%7D%7D/[Neo4j
Manual]
